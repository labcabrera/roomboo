buildscript {
  ext {
    springfoxSwaggerVersion      = '2.9.2'
    jsonwebtokenVersion          = '0.9.1'
  }
}

plugins {
	id 'java-library'
	id 'maven-publish'
	id 'org.springframework.boot' version '2.1.0.RELEASE'  apply false
	id 'com.palantir.docker'      version '0.20.1'         apply false
}

subprojects {
  apply plugin: 'java-library'
  apply plugin: 'maven-publish'
  apply plugin: 'io.spring.dependency-management'

  group = 'org.lab.roomboo'
  version = version
  sourceCompatibility = 1.8

  repositories {
    mavenCentral()
  }
  dependencies {
    compileOnly('org.projectlombok:lombok')
    testCompile('org.springframework.boot:spring-boot-starter-test')
  }
  
}

project(":roomboo-domain") {
  dependencies {
    api('org.springframework.boot:spring-boot-starter-data-mongodb')
    api('org.springframework.boot:spring-boot-starter-security')
    api('org.springframework.boot:spring-boot-starter-validation')
    api('org.apache.commons:commons-lang3')
  }
}

project(":roomboo-core") {
  dependencies {
    api(project(":roomboo-domain"))
    api('org.springframework.boot:spring-boot-starter-integration') 
    api('org.springframework.boot:spring-boot-starter-thymeleaf')
    api('org.springframework.boot:spring-boot-starter-mail')
    api('org.springframework.integration:spring-integration-amqp')
    api('org.springframework.integration:spring-integration-amqp')
    testCompile('com.fasterxml.jackson.core:jackson-databind')
  }
}

project(":roomboo-jwt") {
  dependencies {
    api(project(":roomboo-domain"))
    api('org.springframework.boot:spring-boot-starter-security')
    api('org.springframework.boot:spring-boot-starter-web')
    api("io.jsonwebtoken:jjwt:${jsonwebtokenVersion}")
  }
}

project(":roomboo-api") {
  apply plugin: 'org.springframework.boot'
  apply plugin: 'com.palantir.docker'
  dependencies {
    api(project(":roomboo-core"))
    api(project(":roomboo-jwt"))
    api('org.springframework.boot:spring-boot-starter-actuator')
    api('org.springframework.boot:spring-boot-starter-hateoas')
    api('org.springframework.boot:spring-boot-starter-web')
    api("io.springfox:springfox-swagger2:${springfoxSwaggerVersion}")
    api("io.springfox:springfox-swagger-ui:${springfoxSwaggerVersion}")    
    testCompile('org.springframework.boot:spring-boot-starter-test')
  }
  springBoot {
    buildInfo()
  }
  docker {
    name "labcabrera/${jar.baseName}"
    tags 'latest'
    files jar.archivePath
    buildArgs(['JAR_FILE': "${jar.archiveName}"])
  }
}

configure(subprojects.findAll { it.name != "roomboo-api" }) {
	dependencyManagement {
		imports {
			mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
		}
	}
	task sourcesJar(type: Jar) {
		from sourceSets.main.allJava
		classifier = 'sources'
	}
	publishing {
		publications {
			mavenJava(MavenPublication) {
				groupId = group
				artifactId = project.name
				version = version
				from components.java
				artifact sourcesJar
			}
		}
	}
}
