buildscript {
  ext {
    springBootVersion            = '2.0.3.RELEASE'
    springfoxSwaggerVersion      = '2.8.0'
    palantirPluginVersion        = '0.20.1'
  }
  repositories {
    mavenCentral()
    maven { url "https://plugins.gradle.org/m2/" }
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    classpath("gradle.plugin.com.palantir.gradle.docker:gradle-docker:${palantirPluginVersion}")
  }
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'eclipse'
  apply plugin: 'org.springframework.boot'
  apply plugin: 'io.spring.dependency-management'

  group = 'org.lab.roomboo'
  version = version
  sourceCompatibility = 1.8

  repositories {
    mavenCentral()
  }
}

project(":roomboo-domain") {
  dependencies {
    compile('org.springframework.boot:spring-boot-starter-data-mongodb')
    compile('org.projectlombok:lombok')
    compile('org.apache.commons:commons-lang3')
    testCompile('org.springframework.boot:spring-boot-starter-test')
  }
  jar {
    enabled = true
  }
  bootJar {
    classifier = 'boot'
    // workaround to create a simple java library instead of a spring boot jar
    mainClassName = 'org.lab.roomboo.Dummy'
  }
}

project(":roomboo-api") {
  apply plugin: 'com.palantir.docker'
  dependencies {
    compile(project(":roomboo-domain"))
    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile('org.springframework.boot:spring-boot-starter-hateoas')
    //compile('org.springframework.boot:spring-boot-starter-security')
    compile('org.springframework.boot:spring-boot-starter-web')    
    compile("io.springfox:springfox-swagger2:${springfoxSwaggerVersion}")
    compile("io.springfox:springfox-swagger-ui:${springfoxSwaggerVersion}")
    
    testCompile('org.springframework.boot:spring-boot-starter-test')
    //testCompile('org.springframework.security:spring-security-test')
  }
  
   docker {
    name "labcabrera/${jar.baseName}"
    tags 'latest'
    files jar.archivePath
    buildArgs(['JAR_FILE': "${jar.archiveName}"])
  }

}



